plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

group = 'com.scalbox'
version = '2.0.0-alpha.2'

// Dependency versions
ext {
    lombokVersion = '1.18.42'
    logbackVersion = '1.5.21'
    guavaVersion = '33.5.0-jre'
    commonsTextVersion = '1.14.0'
    javaWebsocketVersion = '1.5.3'
    jacksonDatabindVersion = '2.20.1'
    reflectionsVersion = '0.9.12'
    junitVersion = '5.9.2'
    junitPlatformLauncher = '1.9.2'
    mockitoVersion = '5.20.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven {
        url = uri('https://jitpack.io')
    }
}

dependencies {
    // JSpecify
    implementation 'org.jspecify:jspecify:1.0.0'
    
    // Lombok
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // Lombok for tests
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    // Runtime dependencies
    implementation "ch.qos.logback:logback-classic:${logbackVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "org.apache.commons:commons-text:${commonsTextVersion}"
    implementation "org.java-websocket:Java-WebSocket:${javaWebsocketVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonDatabindVersion}"
    implementation "org.reflections:reflections:${reflectionsVersion}"

    // Test dependencies
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:${junitPlatformLauncher}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
}

test {
    useJUnitPlatform()
}

tasks.withType(Javadoc).configureEach {
    failOnError = false
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            // Artifact ID (nome del tuo progetto)
            artifactId = 'rust-rcon'

            pom {
                name = 'rust-rcon'
                description = 'An async, fault-tolerant Java RCON client for the Rust game. Seamlessly integrate with Rust\'s RCON over websockets, translating game events into actionable insights.'
                url = 'https://github.com/scalbox/rust-rcon'
                inceptionYear = '2025'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = '1'
                        name = 'Scalbox'
                        email = 'info@scalbox.com'
                        organization = 'Scalbox'
                        organizationUrl = 'https://scalbox.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/scalbox/rust-rcon.git'
                    developerConnection = 'scm:git:ssh://github.com:scalbox/rust-rcon.git'
                    url = 'https://github.com/scalbox/rust-rcon'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/scalbox/rust-rcon/issues'
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv("GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("GPG_PASSPHRASE")

    if (signingKey != null && signingPassword != null) {
        useInMemoryPgpKeys(signingKey, signingPassword)
    }

    sign publishing.publications.mavenJava
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = System.getenv("OSSRH_USERNAME")
            password = System.getenv("OSSRH_PASSWORD")
        }
    }

    // Timeout per operazioni lente
    connectTimeout.set(Duration.ofMinutes(3))
    clientTimeout.set(Duration.ofMinutes(3))

    // Transizione automatica dopo il close
    transitionCheckOptions {
        maxRetries.set(50)
        delayBetween.set(Duration.ofSeconds(10))
    }
}

// Task per verificare la configurazione
tasks.register('verifyPublishConfig') {
    doLast {
        println "Group: ${project.group}"
        println "Artifact: ${publishing.publications.mavenJava.artifactId}"
        println "Version: ${project.version}"
        println "OSSRH Username configured: ${project.findProperty("ossrhUsername") ?: System.getenv('OSSRH_USERNAME') != null}"
        println "OSSRH Password configured: ${project.findProperty("ossrhPassword") ?: System.getenv('OSSRH_PASSWORD') != null}"
        println "GPG Key configured: ${project.findProperty("gpgPrivateKey") ?: System.getenv('GPG_PRIVATE_KEY') != null}"
        println "GPG Passphrase configured: ${project.findProperty("gpgPassphrase") ?: System.getenv('GPG_PASSPHRASE') != null}"
    }
}

//publishing {
//    publications {
//        mavenJava(MavenPublication) {
//            from components.java
//        }
//    }
//    repositories {
//        maven {
//            name = 'GitHubPackages'
//            url = uri('https://maven.pkg.github.com/Scalbox/rust-rcon')
//            credentials {
//                username = project.findProperty('gpr.user') ?: System.getenv('GITHUB_ACTOR')
//                password = project.findProperty('gpr.key') ?: System.getenv('GITHUB_TOKEN')
//            }
//        }
//    }
//}